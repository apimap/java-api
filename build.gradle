plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version "2.5.4"
    id 'maven-publish'
    id "com.github.johnrengelman.processes" version "0.5.0"
    id "org.springdoc.openapi-gradle-plugin" version "1.3.3"
}

group = 'io.apimap.api'
sourceCompatibility = 13.0

processResources {
    expand(project.properties)
}

repositories {
    maven {
        url = uri("https://maven.pkg.github.com/apimap/java-rest-interface")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("MULTI_PACKAGES_USER")
            password = project.findProperty("gpr.key") ?: System.getenv("MULTI_PACKAGES_TOKEN")
        }
    }
    mavenCentral()
}

tasks.withType( JavaCompile ).configureEach {
    options.forkOptions.jvmArgs.addAll( ['--add-opens', 'jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED'] )
}

dependencies {
    // Spring Boot Dependencies
    implementation('org.springframework.boot:spring-boot-starter-webflux:2.5.5') {
        exclude module: 'spring-boot-starter-reactor-netty'
    }

    implementation 'org.springframework.boot:spring-boot-starter-jetty:2.5.5'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:2.5.5'
    implementation 'org.springframework.boot:spring-boot-starter-security:2.5.5'

    implementation 'org.springdoc:springdoc-openapi-webflux-ui:1.5.11'
    implementation 'org.eclipse.jetty:jetty-reactive-httpclient:1.1.4'
    implementation 'org.dizitart:nitrite:3.4.3'

    implementation 'io.apimap.api:rest-interface:2.0-SNAPSHOT'

    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.5.5'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
}

jacoco {
    toolVersion = "0.8.6"
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}

bootRun {
    if (project.hasProperty('args')) {
        args project.args.split(',')
    }
}

test {
    useJUnitPlatform()
}

openApi {
    apiDocsUrl.set("http://localhost:8080/v3/api-docs/")
    outputDir.set(file("$projectDir/"))
    outputFileName.set("swagger.json")
    waitTimeInSeconds.set(10)
    forkProperties.set("-Dspring.profiles.active=swaggermock")
}