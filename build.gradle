plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.4'
    id 'maven-publish'
    id 'com.github.johnrengelman.processes' version '0.5.0'
    id 'org.springdoc.openapi-gradle-plugin' version '1.4.0'
    id 'de.gliderpilot.semantic-release' version '1.4.2'
    id 'com.github.spotbugs' version '5.0.12'
}

group = 'io.apimap.api'

processResources {
    expand(project.properties)
}

repositories {
    maven {
        url = uri("https://maven.pkg.github.com/apimap/java-rest-interface")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("MULTI_PACKAGES_USER")
            password = project.findProperty("gpr.key") ?: System.getenv("MULTI_PACKAGES_TOKEN")
        }
    }
    mavenCentral()
}

tasks.withType( JavaCompile ).configureEach {
    options.forkOptions.jvmArgs.addAll( ['--add-opens', 'jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED'] )
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux:2.7.4'
    implementation 'org.springframework.boot:spring-boot-starter-jetty:2.7.4'
    implementation 'org.springframework.boot:spring-boot-starter-actuator:2.7.4'
    implementation 'org.springframework.boot:spring-boot-starter-security:2.7.4'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb-reactive:2.7.4'
    implementation 'org.springdoc:springdoc-openapi-webflux-ui:1.6.11'
    implementation 'com.github.spotbugs:spotbugs-annotations:4.7.2'
    implementation 'io.netty:netty-buffer:4.1.82.Final'
    implementation 'org.dizitart:nitrite:3.4.4'
    implementation 'org.commonmark:commonmark:0.19.0'
    implementation 'io.apimap.api:rest-interface:2.2.0'
    implementation 'io.micrometer:micrometer-core:1.9.4'

    runtimeOnly 'io.micrometer:micrometer-registry-prometheus:1.9.4'

    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.7.4'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
    testImplementation 'org.assertj:assertj-core:3.23.1'
}

bootRun {
    if (project.hasProperty('args')) {
        args project.args.split(',')
    }
}

bootJar {
    archiveName 'api-complete.jar'
}

jar{
    archiveName 'api.jar'
}

test {
    useJUnitPlatform()
}

semanticRelease {
    releaseBranches {
        include 'master'
    }
    repo {
        ghToken = System.getenv('GITHUB_TOKEN')
    }
}

openApi {
    apiDocsUrl.set("http://localhost:8080/documentation/openapi3")
    outputDir.set(file("$projectDir/"))
    outputFileName.set("swagger.json")
    waitTimeInSeconds.set(10)
    customBootRun {
        args.set(["--spring.profiles.active=swaggermock"])
    }
}